apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'

// Template keystore file
def mockPropertiesFile = rootProject.file("template.properties")

// App keystore file
def appPropertiesFile = rootProject.file("app.properties")
def appProperties = new Properties()

if (appPropertiesFile.exists()) {
    appProperties.load(new FileInputStream(appPropertiesFile))
} else {
    appProperties.load(new FileInputStream(mockPropertiesFile))
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    signingConfigs {
        release {
            keyAlias appProperties['keyAlias']
            keyPassword appProperties['keyPassword']
            storeFile file(appProperties['storeFile'])
            storePassword appProperties['storePassword']
        }
    }
    defaultConfig {
        applicationId "org.immuni.android"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 31
        versionName "1.0.29"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        buildConfigField("String", "sesameOracleKey", appProperties['sesameOracleKey'])
        buildConfigField("String", "sesamePicoKey", appProperties['sesamePicoKey'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(":base")
    implementation project(":concierge")
    implementation project(":secretmenu")
    implementation project(':sesame')
    implementation project(":oracle")
    implementation project(":theirs")
    implementation project(":pico")
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.2.0-alpha04'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.fragment:fragment:1.2.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.work:work-runtime-ktx:2.3.4"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation "androidx.security:security-crypto:1.0.0-beta01"

    // Number Picker
    implementation 'com.shawnlin:number-picker:2.4.8'

    // Room components
    implementation "androidx.room:room-runtime:2.2.4"
    implementation "androidx.room:room-ktx:2.2.4"
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.1'
    kapt "androidx.room:room-compiler:2.2.4"
    androidTestImplementation "androidx.room:room-testing:2.2.4"

    // SQL Cipher
    implementation 'net.zetetic:android-database-sqlcipher:4.3.0'
    implementation "androidx.sqlite:sqlite:2.0.1"

    // Koin
    implementation 'org.koin:koin-android:2.1.4'
    implementation 'org.koin:koin-androidx-scope:2.1.4'
    implementation 'org.koin:koin-androidx-viewmodel:2.1.4'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.github.bumptech.glide:annotations:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    //Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.8.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"
    implementation 'com.squareup.moshi:moshi-adapters:1.8.0'

    //Retrofit2
    implementation "com.squareup.retrofit2:retrofit:2.6.1"
    implementation "com.squareup.retrofit2:converter-moshi:2.6.1"

    //Okhttp3
    implementation "com.squareup.okhttp3:okhttp:4.2.1"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    // Lottie
    implementation 'com.airbnb.android:lottie:3.0.7'

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Firebase FCM
    implementation 'com.google.firebase:firebase-messaging:20.1.0' // 20.1.1 has known issues

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20140107'

    // Koin testing tools
    testImplementation 'org.koin:koin-test:2.0.1'
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
